plugins {
    id 'java-library'
}
dependencies {
  implementation files('libs/swagger-codegen-cli-3.0.21.jar')
}
task genClient(type: JavaExec) {
  main = "io.swagger.codegen.v3.cli.SwaggerCodegen"
  classpath = sourceSets.main.runtimeClasspath
  args("generate","-i","in/in.yaml","-l","java","-c","client.json","-o","clients")
  doLast {
    delete file('clients/settings.gradle')
    delete file('clients/build.gradle')
  }
}

defaultTasks 'genClient'

project(':clients') {
  apply plugin: 'maven'
  apply plugin: 'java-library'
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
  repositories {
    mavenCentral()
    maven{
      url 'c:/maven/repository' // ローカルリポジトリ
    }
  }
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7
  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: "file:/maven/repository/") // file:とすること
        pom.version = '1.0.0'
        pom.artifactId = 'clients'
        pom.groupId = 'jp.co.confrage'
      }
    }
  }
  ext {
    swagger_annotations_version = "2.0.0"
    jackson_version = "2.8.9"
    spring_web_version = "4.3.9.RELEASE"
    jodatime_version = "2.9.9"
    junit_version = "4.12"
    jackson_threeten_version = "2.6.4"
  }
  dependencies {
    compile "io.swagger.core.v3:swagger-annotations:$swagger_annotations_version"
    compile "org.springframework:spring-web:$spring_web_version"
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version"
    compile "com.github.joschi.jackson:jackson-datatype-threetenbp:$jackson_threeten_version"
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    testCompile "junit:junit:$junit_version"
  }
  defaultTasks 'uploadArchives'
}